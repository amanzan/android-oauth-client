apply plugin: 'android'

def computeVersionCode(versionName) {
  def parts = versionName.replaceAll('[^-\\d]', '').replaceAll('[-]$', '').split('-', 3)
  def versionCode = 0.0
  parts.eachWithIndex() { part, i ->
    if (i<2) {
      versionCode += (Math.pow(10, ((1-i) * 4)) * part.toInteger())
    }
  }
  versionCode.toInteger()
}

def gitSha() {
  'git rev-parse --short HEAD'.execute().text.trim()
}

def buildTime() {
  new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))
}

android {
  compileSdkVersion 29
  buildToolsVersion '28.0.3'

  defaultConfig {
    minSdkVersion 24
    targetSdkVersion 29

    versionName rootProject.ext.versionName
    versionCode computeVersionCode(rootProject.ext.versionName)

    buildConfigField 'String', 'GIT_SHA', "\"${gitSha()}\""
    buildConfigField 'String', 'BUILD_TIME', "\"${buildTime()}\""
  }

  buildTypes {
    debug {
      versionNameSuffix '-debug'
    }
  }

  lintOptions {
    // temporary fix for https://code.google.com/p/android/issues/detail?id=64014
    disable 'InvalidPackage'
  }

  sourceSets {
    main {
      manifest.srcFile 'AndroidManifest.xml'
      res.srcDirs = ['res']
    }
  }

  packagingOptions {
    pickFirst  'META-INF/*'
  }
}

//apply plugin: "android-unit-test"

dependencies {
  implementation project(':library')
  implementation('de.keyboardsurfer.android.widget:crouton:1.8.1') {
    exclude group: 'com.google.android', module: 'support-v4'
  }
  implementation 'com.squareup.picasso:picasso:2.2.0'

  testImplementation 'junit:junit:4.12'
  testImplementation 'org.robolectric:robolectric:2.4'
  implementation 'androidx.fragment:fragment:1.2.2'
  implementation 'com.google.oauth-client:google-oauth-client-java6:1.30.5'
  implementation('com.google.api-client:google-api-client-android:1.30.8') {
    exclude group: 'com.google.android.google-play-services', module: 'google-play-services'
    exclude group: 'junit', module: 'junit'
    exclude group: 'com.google.android', module: 'android'
  }
  implementation 'com.google.http-client:google-http-client-jackson:1.29.2'
}

File signFile = rootProject.file('sign.properties')
if (signFile.exists()) {
  Properties p = new Properties()
  p.load(new FileInputStream(signFile))
  android {
    signingConfigs {
      release {
        storeFile file(p.KEYSTORE)
        storePassword p.KEYSTORE_PASSWORD
        keyAlias p.KEY_ALIAS
        keyPassword p.KEY_PASSWORD
      }
    }
  }
  android.buildTypes.release.signingConfig android.signingConfigs.release
}